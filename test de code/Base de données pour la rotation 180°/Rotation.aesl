<!DOCTYPE aesl-source>
<network>


<!--list of global events-->
<event size="6" name="rotation"/>


<!--node Thymio sur DESKTOP-V5RDJ31 - 776-->
<node name="Thymio sur DESKTOP-V5RDJ31 - 776" nodeId="{6c634174-76f5-4aff-9bc9-4c200445c7aa}"><![CDATA[var etatg
var etatd
var switch=0
var inc=0
var speedcms=0
var impaire=1 #le impaire permet d'osciller entre 23 et 22 incrémentations (qui correspond à chaque 1 cm/s (voir excel calibrage)
var colour=10 #permet de vsualiser la modification de la vitesse aux roues
timer.period[1]=100 #la fréquence de lecture de prox ground
onevent timer1
etatg=prox.ground.delta[0] #lecture gauche
etatd=prox.ground.delta[1] #lecture droite
emit rotation [etatg,etatd,switch,speedcms,colour,inc]

if  switch==1 then #quand on appuye une fois sur button.center, le robot tourne sur lui même sans s'arrêter
	motor.left.target=speedcms
	motor.right.target=-speedcms
	if  etatd>=200 then
		inc++
		call leds.top(0,32,0) #lis l'incrément et signale sa lecture
	else
	call leds.top(0,0,32)	#ne lis pas l'incrément et signale sa non lecture
	end
	
end

onevent button.center
if button.center==1 then #incrémente le mode à chaque fois qu'on appuye sur le bouton central
	switch++
end
if  switch==2 then #remet le mode à 0 quand le mode est à 2 (donc on a que 2 modes)
	switch=0
end

onevent button.right #incrémente en cm/s
if  switch==0 and button.right==1 then
		if impaire==1 then
		speedcms+=23
		impaire=0
		colour++
		else
		speedcms+=22
		impaire=1
		colour++
		end
		if speedcms>500 then
			speedcms=0
			impaire=1
			colour=10
		end
		call leds.top(colour,0,0)
end

onevent button.left #décremente en cm/s
if  switch==0 and button.left==1 then
		if impaire==1 then
		speedcms-=22
		impaire=0
		colour--
		else
		speedcms-=23
		impaire=1
		colour--
		end
		if speedcms<0 then
			speedcms=495
			impaire=1
			colour=32
		end
		call leds.top(colour,0,0)
end
onevent button.backward #arrêt manuel basé sur n'importe quelle valeur détectée par les capteurs de proximité
	if   (etatg>=0 and etatd >=0) then
		motor.right.target=0
		motor.left.target=0
		call leds.top(32,32,0)
		colour=10
		speedcms=0
		impaire=1
		switch=0
	end
onevent button.forward
if switch==0 then #remise manuelle de l'incrément à 0 lorsqu'on est en mode 1 (afin d'effectuer d'autres lectures de temps de rotation)
	inc=0
end
	]]></node>


</network>
